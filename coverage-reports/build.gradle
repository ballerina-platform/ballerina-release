/*
 * Copyright (c) 2021, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
apply plugin: 'base'
apply plugin: 'jacoco'

configurations {
    exec
    jar {
        transitive = false
    }
}

dependencies {
    exec group: 'org.ballerinalang',  name: 'project-api-test', version: '2.0.0-alpha3-SNAPSHOT', ext: "exec", classifier: 'jacoco'
    exec group: 'org.ballerinalang',  name: 'testerina-integration-test', version: '2.0.0-alpha3-SNAPSHOT', ext: "exec", classifier: 'jacoco'

    jar group: 'org.ballerinalang',  name: 'ballerina-cli', version: '2.0.0-alpha3-SNAPSHOT'
    jar group: 'org.ballerinalang',  name: 'ballerina-lang', version: '2.0.0-alpha3-SNAPSHOT'
}

repositories {
    mavenLocal()

    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
    maven {
         name = "GitHubPackages"
         url = uri("https://maven.pkg.github.com/ballerina-platform/module-ballerina-graphql")
         credentials {
           username = System.getenv("packageUser")
           password = System.getenv("packagePAT")
         }
     }
}

task getDeps(type: Copy) {
    from configurations.exec
    into "$buildDir/jacoco"
}

task extractClassFiles(type: Copy) {
    for(file in configurations.jar) {
        from zipTree(file).matching {
            include '**/*.class'
            exclude 'module-info.class'
        }
        into "$buildDir/classes"
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree("$buildDir/jacoco/").matching {
        include "**.exec"
    }
    additionalClassDirs files("$buildDir/classes/")
    reports {
        html.enabled true
    }
}

codeCoverageReport.dependsOn(getDeps)
codeCoverageReport.dependsOn(extractClassFiles)

group 'io.ballerina'
version '1.0-SNAPSHOT'
